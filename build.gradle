plugins {
    id 'java'
    id 'org.kordamp.gradle.jandex' version '1.0.0'
    id 'application'
}

group 'com.distribuida'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    implementation platform("io.helidon:helidon-dependencies:3.2.6")

    implementation('io.helidon.microprofile.server:helidon-microprofile-server')
    implementation('io.helidon.microprofile.bundles:helidon-microprofile')
    implementation('org.glassfish.jersey.media:jersey-media-json-binding')

    runtimeOnly('org.jboss:jandex')
    runtimeOnly('jakarta.activation:jakarta.activation-api')

    testImplementation('io.helidon.microprofile.tests:helidon-microprofile-tests-junit5')
    testCompileOnly('org.junit.jupiter:junit-jupiter-api:')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:')
}

application {
    mainClass = 'io.helidon.microprofile.cdi.Main'
}

test {
    useJUnitPlatform()
}

task copyLibs(type: Copy) {
    from configurations.runtimeClasspath
    into 'build/libs/libs'
}

copyLibs.dependsOn jar
assemble.dependsOn copyLibs

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveFileName = "${project.name}.jar"
    manifest {
        attributes (
                'Main-Class': "${application.mainClass}",
                'Class-Path': configurations.runtimeClasspath.files.collect { "libs/$it.name" }.join(' ')
        )
    }
}

task copyBeansXML {
  doLast {
    ant.copy file: "${projectDir}/src/main/resources/META-INF/beans.xml",
             todir: "${buildDir}/classes/java/main/META-INF"
  }
}

classes.dependsOn copyBeansXML